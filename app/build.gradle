apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "it.dindonkey.chucknorrisjokes"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        mock {
            buildConfigField 'int', 'CHUCK_API_NETWORK_BEHAVIOUR_DELAY', '2'
            buildConfigField 'int', 'CHUCK_API_NETWORK_BEHAVIOUR_FAILURE_PERCENT', '0'
            buildConfigField 'int', 'CHUCK_API_NETWORK_BEHAVIOUR_VARIANCE_PERCENT', '0'

            buildConfigField 'int', 'GIPHY_API_NETWORK_BEHAVIOUR_DELAY', '5'
            buildConfigField 'int', 'GIPHY_API_NETWORK_BEHAVIOUR_FAILURE_PERCENT', '0'
            buildConfigField 'int', 'GIPHY_API_NETWORK_BEHAVIOUR_VARIANCE_PERCENT', '0'
        }

        prod {

        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    sourceSets {
        String sharedAssets = 'src/sharedTest/assets'
        test {
            java.srcDirs = ['src/test/java', 'src/sharedTest/java']
            assets.srcDirs = [sharedAssets]
            resources.srcDirs = [sharedAssets]
        }

        androidTest {
            java.srcDirs = ['src/androidTest/java', 'src/sharedTest/java']
            assets.srcDirs = [sharedAssets]
            resources.srcDirs = [sharedAssets]
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

//noinspection GroovyAssignabilityCheck
configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:23.4.0"
}

//noinspection GroovyAssignabilityCheck
task testJVM(dependsOn: 'testProdDebugUnitTest',
        description: 'Runs JVM unit test',
        group: 'Verification')

//noinspection GroovyAssignabilityCheck
task testAndroid(dependsOn: 'connectedProdDebugAndroidTest',
        description: 'Runs Android unit test',
        group: 'Verification')

//noinspection GroovyAssignabilityCheck
task testAll(dependsOn: ['testJVM', 'testAndroid'],
        description: 'Runs both JVM and Android unit test',
        group: 'Verification')

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.5'

    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile 'com.squareup.retrofit2:retrofit-mock:2.0.2'
    compile 'com.squareup.okhttp3:mockwebserver:3.3.0'
    compile 'commons-io:commons-io:2.5'
    compile 'com.android.support:recyclerview-v7:23.2.1'
    compile 'com.android.support:cardview-v7:23.2.1'
    compile 'org.parceler:parceler-api:1.1.5'

    compile 'com.github.bumptech.glide:glide:3.7.0'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
        exclude group: 'com.android.support'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'

    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile('com.google.dexmaker:dexmaker-mockito:1.2') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    apt 'org.parceler:parceler:1.1.5'
}
